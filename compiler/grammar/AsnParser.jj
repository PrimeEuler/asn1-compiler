/*
 *  ASN.1 grammar  for JavaCC
 *
 *  Contributed by Helena Sarin (hsarin@lucent.com)
 *  Modified by Mikhail Kulinich (tysonite@gmail.com)
 *
 *  Derived in part from the following work: snacc - a freeware ASN.1 to C or C++ compiler, v 1.3,
 *  yacc/lex source code ( parse-asn1.y, lex-asn1.l), 
 *  the free software, which is covered by GNU General Public License as published by
 *  the Free Software Foundation; either version 2 of the License, or later
 *
 *  I tested it against several ASN.1 files used by CMIP;
 *  Additional work is reguired to fully support SNMP MIBs parsing
 *   
 *  Please let me know if you use this grammar: i'm using it to develop ASN.1/IDL compiler
 *  
 */

options {
SANITY_CHECK=true;
DEBUG_PARSER=false;
DEBUG_TOKEN_MANAGER=false;
MULTI=true;
VISITOR=true;
NODE_SCOPE_HOOK=true;
OUTPUT_DIRECTORY="..\\src\\parser";
NODE_PACKAGE="parser";
}

PARSER_BEGIN(AsnParser)
package parser;

public class AsnParser
{
   public static void jjtreeOpenNodeScope(Node n) {
      ((SimpleNode) n).setFirstToken(getToken(1));
   }

   public static void jjtreeCloseNodeScope(Node n) {
      ((SimpleNode) n).setLastToken(getToken(0));
   }

   public SimpleNode getTreeRootNode() {
      return (SimpleNode) jjtree.rootNode();
   }
}

PARSER_END(AsnParser)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

SPECIAL_TOKEN :
{
  "--" : WithinASNComment
}

<WithinASNComment> SPECIAL_TOKEN :
{
  <("--" | "\n" | "\r" | "\r\n" )> : DEFAULT
}

<WithinASNComment> MORE :
{
  <~[]>
}

TOKEN : 
{
 <OBJECT_IDENTIFIER_TKN: "OBJECT" ([" ","\n","\t","\r"])? "IDENTIFIER">
}

TOKEN : 
{
 <BSTRING_TKN: "\'" (["0"-"1"])* "\'B">
}

TOKEN : 
{
 <HSTRING_TKN: "\'" (["0"-"9","A"-"F","a"-"f"])* "\'H">
}

TOKEN : 
{
 <CSTRING_TKN: "\"" ( ~["\""] )* "\"" >
}

TOKEN : 
{
 <NUMBER_ERANGE_TKN: ["1"-"9"](["0"-"9"])*>
}

TOKEN : 
{
 <NUMBER_TKN: "0">
}

TOKEN :
{
  <DOT_TKN: ".">
| <COMMA_TKN: ",">
| <LEFTBRACE_TKN: "{">
| <RIGHTBRACE_TKN: "}">
| <LEFTPAREN_TKN: "(">
| <RIGHTPAREN_TKN: ")">
| <LEFTBRACKET_TKN: "[">
| <RIGHTBRACKET_TKN: "]">
| <LESSTHAN_TKN: "<">
| <MINUS_TKN: "-">
| <GETS_TKN: "::=">
| <BAR_TKN: "|">
| <SEMI_COLON_TKN: ";">
| <SELECTOR_TKN: ":">
| <TAGS_TKN: "TAGS">
| <BOOLEAN_TKN: "BOOLEAN">
| <INTEGER_TKN: "INTEGER">
| <BIT_TKN: "BIT">
| <STRING_TKN: "STRING">
| <OCTET_TKN: "OCTET">
| <NULL_TKN: "NULL">
| <SEQUENCE_TKN: "SEQUENCE">
| <OF_TKN: "OF">
| <SET_TKN: "SET">
| <IMPLICIT_TKN: "IMPLICIT">
| <CHOICE_TKN: "CHOICE">
| <ANY_TKN: "ANY">
| <PRINTABLESTRING_TKN: "PrintableString">
| <GENERALIZEDTIME_TKN: "GeneralizedTime">
| <UTCTIME_TKN: "UTCTime">
| <GRAPHICSTRING_TKN: "GraphicString">
| <IA5STRING_TKN: "IA5String">
| <NUMERICSTRING_TKN: "NumericString">
| <TELETEXSTRING_TKN: "TeletexString">
| <OPTIONAL_TKN: "OPTIONAL">
| <DEFAULT_TKN: "DEFAULT">
| <COMPONENTS_TKN: "COMPONENTS">
| <UNIVERSAL_TKN: "UNIVERSAL">
| <APPLICATION_TKN: "APPLICATION">
| <PRIVATE_TKN: "PRIVATE">
| <TRUE_TKN: "TRUE">
| <FALSE_TKN: "FALSE">
| <BEGIN_TKN: "BEGIN">
| <END_TKN: "END">
| <DEFINITIONS_TKN: "DEFINITIONS">
| <EXPLICIT_TKN: "EXPLICIT">
| <AUTOMATIC_TKN: "AUTOMATIC">
| <ENUMERATED_TKN: "ENUMERATED">
| <EXTERNAL_TKN: "EXTERNAL">
| <EXPORTS_TKN: "EXPORTS">
| <IMPORTS_TKN: "IMPORTS">
| <REAL_TKN: "REAL">
| <INCLUDES_TKN: "INCLUDES">
| <CONTAINING_TKN: "CONTAINING">
| <MIN_TKN: "MIN">
| <MAX_TKN: "MAX">
| <SIZE_TKN: "SIZE">
| <FROM_TKN: "FROM">
| <WITH_TKN: "WITH">
| <COMPONENT_TKN: "COMPONENT">
| <PRESENT_TKN: "PRESENT">
| <ABSENT_TKN: "ABSENT">
| <DEFINED_TKN: "DEFINED">
| <BY_TKN: "BY">
| <PLUS_INFINITY_TKN: "PLUS-INFINITY">
| <MINUS_INFINITY_TKN: "MINUS-INFINITY">
| <OBJECTTYPE_TKN: "OBJECT-TYPE">
| <SYNTAX_TKN: "SYNTAX">
| <ACCESS_TKN: "ACCESS">
| <STATUS_TKN: "STATUS">
| <DESCRIPTION_TKN: "DESCRIPTION">
| <REFERENCE_TKN: "REFERENCE">
| <INDEX_TKN: "INDEX">
| <DEFVAL_TKN: "DEFVAL">
| <DHINT: "DISPLAY-HINT">
| <TC_TKN: "TEXTUAL-CONVENTION"> 
| <MACRO_TKN: "MACRO"> 
}

TOKEN : 
{
 <LCASEFIRST_IDENT_TKN: ["a"-"z"] (("-"["a"-"z","A"-"Z","0"-"9"])|["a"-"z","A"-"Z","0"-"9"])*>
}


TOKEN : 
{
 <UCASEFIRST_IDENT_TKN: ["A"-"Z"](("-"["a"-"z","A"-"Z","0"-"9"])|["a"-"z","A"-"Z","0"-"9"])*>
}

void Input() : {} 
{ 
 (ModuleDefinition())*
}

void ModuleDefinition() :
{
   int tag;
} 
{ 
   ModuleIdentifier() <DEFINITIONS_TKN>
   [
      tag = TagDefault()
      {
         jjtThis.setTag(tag);
      }
   ]
   <GETS_TKN> <BEGIN_TKN> ModuleBody() <END_TKN>
}

int TagDefault() :
{
   int tag;
}
{ 	
   (
    <EXPLICIT_TKN> { tag = ASTModuleDefinition.EXPLICIT; }
    |
    <IMPLICIT_TKN> { tag = ASTModuleDefinition.IMPLICIT; }
    |
    <AUTOMATIC_TKN> { tag = ASTModuleDefinition.AUTOMATIC; }
   )
   <TAGS_TKN>
   {
      return tag;
   }
}

void ModuleIdentifier() : {  } 
{ 
   modulereference() [AssignedIdentifier()]
}

void AssignedIdentifier() : {} 
{ 
   ObjectIdentifierValue() 
}

void ModuleBody() : {} 
{ 
  [Exports()] [Imports()] [AssignmentList()] 
}

void Exports() : {} 
{ 
  <EXPORTS_TKN> [SymbolsExported()] <SEMI_COLON_TKN> 
}

void SymbolsExported() : {} 
{ 
   ExportSymbolList() 
}

void ExportSymbolList() : {} 
{ 
   Symbol() (<COMMA_TKN> Symbol())*
}

void Imports() : {} 
{ 
   <IMPORTS_TKN> [SymbolsImported()] <SEMI_COLON_TKN> 
}

void SymbolsImported() : {} 
{ 
   SymbolsFromModuleList() 
}

void SymbolsFromModuleList() : {} 
{ 
   SymbolsFromModule() (SymbolsFromModule())*
}

void SymbolsFromModule() : { String module; } 
{ 
   SymbolList() <FROM_TKN>  ModuleIdentifier() 
}

void SymbolList() : {} 
{ 
   Symbol() (<COMMA_TKN> Symbol())* 
}

void Symbol() : {} 
{ 
   typereference()
 | identifier() 
 | DefinedMacroName() 
}

void AssignmentList() : {} 
{ 
  Assignment() [<SEMI_COLON_TKN>] ( Assignment() [<SEMI_COLON_TKN>] )*
}

void Assignment() #void: {} 
{ 
   LOOKAHEAD(2)  MacroDefinition() |  TypeAssignment() |  ValueAssignment()
}

void MacroDefinition() : { } 
{
   (<TC_TKN> | <OBJECTTYPE_TKN> | typereference()) <MACRO_TKN> <GETS_TKN> 
	<BEGIN_TKN>  MacroBody() <END_TKN>
}

JAVACODE
void MacroBody() {	
  Token tok;
  int nesting = 1;
  while (true) {
    tok = getToken(1);
    if (tok.kind == END_TKN) {
	break;	
    }
    tok = getNextToken();
  }
}

void MacroReference() : {} 
{ 
   typereference() | DefinedMacroName() 
}

void TypeAssignment() :
{
   int type;
}
{
   typereference() <GETS_TKN>
   (
      type = GlobalType()
      {
         jjtThis.setAssignment(type);
      }
   )
}

int GlobalType() #void:
{
   int type = -1;
}
{
   (type = Type() | DefinedMacroType())
   {
      return type;
   }
}

int Type() #void: { int type = -1; }
{ 
   (LOOKAHEAD(2)
      BuiltinType()
      {
         type = ASTTypeAssignment.BUILTIN_ASSIGNMENT;
      }
      |
      DefinedType()
      {
         type = ASTTypeAssignment.DEFINED_ASSIGNMENT;
      }
   ) [LOOKAHEAD(2) SubtypeSpec()]
   { return type; }
}

void DefinedType() : { } 
{  
  [LOOKAHEAD(2) modulereference() <DOT_TKN>]  typereference() 
}

void BuiltinType() : { int x; } 
{ 
  IntegerType() { jjtThis.setType(0); }
| BitStringType()  { jjtThis.setType(1); }
| LOOKAHEAD(2) x = SetOrSequenceType() { jjtThis.setType(x); }
| x = SetOrSequenceOfType() { jjtThis.setType(x); }
| ChoiceType() { jjtThis.setType(6); }
| SelectionType() { jjtThis.setType(7); }
| TaggedType() { jjtThis.setType(8); }
| AnyType() { jjtThis.setType(9); }
| EnumeratedType() { jjtThis.setType(10); }
| OctetStringType() { jjtThis.setType(11); }
| ObjectIdentifierType() { jjtThis.setType(12); }
| <REAL_TKN> { jjtThis.setType(13); }
| BooleanType() { jjtThis.setType(14); }
| NullType() { jjtThis.setType(15); }
| <EXTERNAL_TKN> { jjtThis.setType(16); }
}

void NullType() : {}
{
	<NULL_TKN>
}

void BooleanType() : {}
{
	<BOOLEAN_TKN>
}

void ObjectIdentifierType() : {}
{
	<OBJECT_IDENTIFIER_TKN>
}

void OctetStringType() : {}
{
	((<OCTET_TKN> <STRING_TKN>) { jjtThis.setType(ASTOctetStringType.StringType.OCTET_STRING); }
         |
         <PRINTABLESTRING_TKN> { jjtThis.setType(ASTOctetStringType.StringType.PRINTABLE_STRING); }
         |
         <GENERALIZEDTIME_TKN> { jjtThis.setType(ASTOctetStringType.StringType.GENERALIZED_TIME); }
         |
         <UTCTIME_TKN> { jjtThis.setType(ASTOctetStringType.StringType.UTC_TIME); }
         |
         <GRAPHICSTRING_TKN> { jjtThis.setType(ASTOctetStringType.StringType.GRAPHIC_STRING); }
         |
         <IA5STRING_TKN> { jjtThis.setType(ASTOctetStringType.StringType.IA5_STRING); }
         |
         <NUMERICSTRING_TKN> { jjtThis.setType(ASTOctetStringType.StringType.NUMERIC_STRING); }
         |
         <TELETEXSTRING_TKN> { jjtThis.setType(ASTOctetStringType.StringType.TELETEX_STRING); }
        )
}

void EnumeratedType() : {} 
{ 
   <ENUMERATED_TKN> <LEFTBRACE_TKN> NamedNumberList() <RIGHTBRACE_TKN>
}

void IntegerType() : {} 
{ 
 <INTEGER_TKN> [ <LEFTBRACE_TKN> NamedNumberList() <RIGHTBRACE_TKN> ]
}

void BitStringType() : {} 
{ 
  <BIT_TKN> <STRING_TKN> [ <LEFTBRACE_TKN> NamedNumberList()  <RIGHTBRACE_TKN> ] 
}

void NamedNumberList() : {  } 
{ 
  NamedNumber() (<COMMA_TKN> NamedNumber())*
}

void NamedNumber() : { } 
{ 
  identifier() [<LEFTPAREN_TKN> ( SignedNumber() | DefinedValue())
                            <RIGHTPAREN_TKN>]
}

void SignedNumber() :
{
   Token m = null, n = null;
} 
{ 
  [ m = <MINUS_TKN> ] ( n = <NUMBER_TKN> | n = <NUMBER_ERANGE_TKN> )
  {
     jjtThis.setNumber(((null != m) ? m.toString() : "") + n.toString());
  }
}

/*
 * modified to return an integer differentiating sets and sequences
 */
int SetOrSequenceOfType() : { int rc; }
{ 
   ( ( <SET_TKN> { jjtThis.setType(ASTSetOrSequenceOfType.SET); rc = 4; }
     | <SEQUENCE_TKN> { jjtThis.setType(ASTSetOrSequenceOfType.SEQUENCE); rc = 5; } ) [[<LEFTPAREN_TKN>] SizeConstraint() [<RIGHTPAREN_TKN>]] <OF_TKN> )
    Type() { return rc; }
}

/*
 * modified to return an integer differentiating sets of and sequences of
 */
int SetOrSequenceType() : { int rc; } 
{ 
  ( ( <SET_TKN> { jjtThis.setType(ASTSetOrSequenceType.SET); rc = 2; }
    | <SEQUENCE_TKN> { jjtThis.setType(ASTSetOrSequenceType.SEQUENCE); rc = 3; }
    ) <LEFTBRACE_TKN> [ ElementTypeList() ] <RIGHTBRACE_TKN> ) { return rc; }
}

void ChoiceType() : { } 
{ 
   <CHOICE_TKN> <LEFTBRACE_TKN> ElementTypeList() <RIGHTBRACE_TKN> 
}

void ElementTypeList() #void: {} 
{ 
   ElementType() (<COMMA_TKN> ElementType())*
}

void DefaultValue() : {}
{
   Value()
}

void ElementType() : {}
{ 
   ( NamedType() [
                  ( <OPTIONAL_TKN> { jjtThis.setOptional(true); } )
                  |
                  ( ( <DEFAULT_TKN> { jjtThis.setDefault(true); } ) DefaultValue() )
                 ]
   )
   |
   ( ComponentsType() )
}

void ComponentsType() : {  }
{
  <COMPONENTS_TKN> <OF_TKN>  Type() 

}

void NamedType() #void: { } 
{ 
  [LOOKAHEAD(2) identifier()]  Type() 
}

void SelectionType() : {} 
{ 
   identifier() <LESSTHAN_TKN> Type() 
}

void TaggedType() : { } 
{ 
  Tag() [ <EXPLICIT_TKN> { jjtThis.setTaggingMethod(ASTTaggedType.EXPLICIT); } | <IMPLICIT_TKN> { jjtThis.setTaggingMethod(ASTTaggedType.IMPLICIT); } ] Type() 
}

void Tag() : {} 
{ 
   <LEFTBRACKET_TKN> [Class()] ClassNumber() <RIGHTBRACKET_TKN> 
}

void ClassNumber() : { int x; } 
{ 
   x = number() { jjtThis.setTagNumber(x); } | DefinedValue()
}

void Class() : {}
{ 
     <UNIVERSAL_TKN> { jjtThis.setTagClass(ASTTaggedType.UNIVERSAL); }
   | <APPLICATION_TKN> { jjtThis.setTagClass(ASTTaggedType.APPLICATION); }
   | <PRIVATE_TKN> { jjtThis.setTagClass(ASTTaggedType.PRIVATE); }
}

void AnyType() : {} 
{ 
  <ANY_TKN> 
  [ <DEFINED_TKN> <BY_TKN> identifier()   ]
}

void SubtypeSpec() : {} 
{ 
   <LEFTPAREN_TKN> SubtypeValueSetList() <RIGHTPAREN_TKN> 
}

void SubtypeValueSetList() : {} 
{ 
   SubtypeValueSet() (<BAR_TKN> SubtypeValueSet())* 
}

/**
 * TODO: implement CONTAINING token
 */
void SubtypeValueSet() : {} 
{ 
   (
      ValueRange() | <CONTAINING_TKN> <UCASEFIRST_IDENT_TKN> | ContainedSubtype() | PermittedAlphabet()
      | SizeConstraint() | InnerTypeConstraints()
   )
}

void ContainedSubtype() : {} 
{ 
   <INCLUDES_TKN> Type() 
}

void SingleValue() : {} 
{ 
   Value() 
}

void ValueRange() : {} 
{ 
   ( Value() | <MIN_TKN> { jjtThis.setMinFlag(true); } ) [ [ <LESSTHAN_TKN> ]  <DOT_TKN> <DOT_TKN> [ <LESSTHAN_TKN> ] (Value() | <MAX_TKN> { jjtThis.setMaxFlag(true); } ) ]
}

void SizeConstraint() : {} 
{ 
   <SIZE_TKN> SubtypeSpec() 
}

void PermittedAlphabet() : {} 
{ 
   <FROM_TKN> SubtypeSpec() 
}

void InnerTypeConstraints() : {} 
{ 
  <WITH_TKN> ((<COMPONENT_TKN> SingleTypeConstraint() ) | ( <COMPONENTS_TKN>  MultipleTypeConstraints()))
}

void SingleTypeConstraint() : {} 
{ 
   SubtypeSpec() 
}

void MultipleTypeConstraints() : {} 
{ 
  <LEFTBRACE_TKN> [ <DOT_TKN> <DOT_TKN> <DOT_TKN> <COMMA_TKN> ] TypeConstraints() <RIGHTBRACE_TKN> 
}

void TypeConstraints() : {} 
{ 
   NamedConstraint() (<COMMA_TKN> NamedConstraint())*
}

void NamedConstraint() : {} 
{ 
    [identifier() ] Constraint() 
}

void Constraint() : {} 
{ 
  [ValueConstraint()] [PresenceConstraint()] 
}

void ValueConstraint() : {} 
{ 
   SubtypeSpec() 
}

void PresenceConstraint() : {} 
{ 
   <PRESENT_TKN> | <ABSENT_TKN> | <OPTIONAL_TKN> 
}



void ValueAssignment() : { } 
{     
    identifier() 
    GlobalType() <GETS_TKN> [ LOOKAHEAD(2) identifier() [<SELECTOR_TKN>] ] [  BuiltinValue() ]
   
}

void Value() : {} 
{ 
   LOOKAHEAD(2) BuiltinValue() | DefinedValue() 
}

void DefinedValue() : {} 
{ 
 [ modulereference() <DOT_TKN> ] identifier()
}

void BuiltinValue() : {} 
{ 
   BooleanValue() 
| NullValue() 
| SpecialRealValue() 
| SignedNumber() 
| HexString() 
| BinaryString() 
| CharString() 
| CompoundValue()
}

void CompoundValue() : {}
{
   <LEFTBRACE_TKN> ( ObjIdComponentList() | skip_to_matching_brace() ) <RIGHTBRACE_TKN> 
}

JAVACODE
void skip_to_matching_brace() {
  Token tok;
  int nesting = 1;
  while (true) {
    tok = getToken(1);
    if (tok.kind == LEFTBRACE_TKN) nesting++;
    if (tok.kind == RIGHTBRACE_TKN) {
      nesting--;
         if (nesting == 0) break;
    }
    tok = getNextToken();
  }
}

void BooleanValue() : {} 
{ 
   <TRUE_TKN> | <FALSE_TKN> 
}

void SpecialRealValue() : {} 
{ 
   <PLUS_INFINITY_TKN> | <MINUS_INFINITY_TKN> 
}

void NullValue() : {} 
{ 
   <NULL_TKN> 
}

void NamedValue() : {} 
{ 
  [ LOOKAHEAD(2) identifier() ] Value() 
}

void ObjectIdentifierValue() : {} 
{ 
   <LEFTBRACE_TKN> ObjIdComponentList() <RIGHTBRACE_TKN> 
}

void ObjIdComponentList() : {} 
{ 
   ObjIdComponent() (ObjIdComponent())*
}

void ObjIdComponent() : {} 
{ 
  ( NumberForm() |   NameAndNumberForm())
}

void NumberForm() : {} 
{ 
   number() 
}


void NameAndNumberForm() : {} 
{ 
  identifier() [<LEFTPAREN_TKN> (NumberForm() | DefinedValue()) <RIGHTPAREN_TKN>] 
}

void BinaryString() : {} 
{ 
   <BSTRING_TKN> 
}

void HexString() : {} 
{ 
   <HSTRING_TKN> 
}

void CharString() : {} 
{ 
   <CSTRING_TKN> 
}

int number() : {}
{ 
   <NUMBER_TKN> { return 0; } | <NUMBER_ERANGE_TKN> { return Integer.parseInt(token.image); }
}

void identifier() : {} 
{ 
   <LCASEFIRST_IDENT_TKN> 
}

void modulereference() : {} 
{ 
   <UCASEFIRST_IDENT_TKN> 
}

void typereference() : {} 
{ 
   <UCASEFIRST_IDENT_TKN>
}

void DefinedMacroType() : {} 
{ 
  TextualConventionMacroType() 
| SnmpObjectTypeMacroType()
}

void DefinedMacroName() : {} 
{ 
  <OBJECTTYPE_TKN> 
| <TC_TKN>
}

void SnmpObjectTypeMacroType() : {} 
{ 
   <OBJECTTYPE_TKN> <SYNTAX_TKN> Type() <ACCESS_TKN> SnmpAccess() <STATUS_TKN> SnmpStatus() [SnmpDescrPart()] [SnmpReferPart()] 
       [SnmpIndexPart()] [SnmpDefValPart()] 
}

void SnmpAccess() : {} 
{ 
   identifier() 
}

void SnmpStatus() : {} 
{ 
  identifier() 
}

void SnmpDescrPart() : {} 
{ 
   <DESCRIPTION_TKN> <CSTRING_TKN>
}

void SnmpReferPart() : {} 
{ 
   <REFERENCE_TKN> <CSTRING_TKN>
}

void SnmpIndexPart() : {} 
{ 
   <INDEX_TKN> <LEFTBRACE_TKN> TypeOrValueList() <RIGHTBRACE_TKN> 
}

void TypeOrValueList() : {} 
{ 
  TypeOrValue() (<COMMA_TKN> TypeOrValue())*
}

void TypeOrValue() : {} 
{ 
  LOOKAHEAD(3) Type() | Value()
}

void SnmpDefValPart() : {} 
{ 
   <DEFVAL_TKN> <LEFTBRACE_TKN> Value() <RIGHTBRACE_TKN> 
}

void TextualConventionMacroType() : {}
{
   <TC_TKN> [DisplayHint()] <STATUS_TKN> SnmpStatus() [SnmpDescrPart()] [SnmpReferPart()] <SYNTAX_TKN> Type()
}

void DisplayHint() : {}
{
  <DHINT> <CSTRING_TKN>
}
